group 'com.pauldemarco.flutter_blue'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.17'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 31
        // Use Java 8 language features and APIs
        // https://developer.android.com/studio/write/java8-support
        multiDexEnabled true
    }
    sourceSets {
        main {
            proto {
                srcDir '../protos'
            }
        }
    }
    compileOptions {
        // Use Java 8 language features and APIs
        // https://developer.android.com/studio/write/java8-support
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

protobuf {
    protoc {
        if (project.hasProperty('protoc_platform')) {
            artifact = "com.google.protobuf:protoc:3.18.0:${protoc_platform}"
        } else {
            artifact = "com.google.protobuf:protoc:3.18.0"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation 'com.google.protobuf:protobuf-javalite:3.18.0'
}